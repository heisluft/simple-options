plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}

group 'de.heisluft.cli'
version '0.3.0'

java {
  toolchain.languageVersion.set(JavaLanguageVersion.of(8))
  withSourcesJar()
  withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:26.0.2'
}

tasks.withType(JavaCompile.class).configureEach {
    options.encoding = 'UTF-8'
}

jar.manifest.attributes.put("Automatic-Module-Name", "de.heisluft.simple-options")

publishing {
    repositories.maven {
        url mavenUrl
        credentials {
            username = mavenUsername
            password = mavenPassword
        }
        authentication.withType(BasicAuthentication)
    }
    publications.create('maven', MavenPublication) {
        it.from components.getByName('java')
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.maven
}